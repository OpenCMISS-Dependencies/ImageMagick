#PROJECT(CORE_magick)

# Include coders source files
include(coders.cmake)

file( GLOB ${LIBRARY_TARGET_NAME}_PRIVATE_HDRS *private.h )
file( GLOB ${LIBRARY_TARGET_NAME}_PUBLIC_HDRS *.h )
file( GLOB ${LIBRARY_TARGET_NAME}_SRCS *.c )
list(REMOVE_ITEM ${LIBRARY_TARGET_NAME}_PUBLIC_HDRS ${${LIBRARY_TARGET_NAME}_PRIVATE_HDRS})

string(TOUPPER ${LIBRARY_TARGET_NAME} UPPERCASE_LIBRARY_TARGET_NAME )

set( IMAGEMAGICK_CONFIG_PREFIX MAGICKCORE_ )
set( HASHCMAKEDEFINE \#cmakedefine )
include( ../ConfigurePrefix.cmake )
set( MAGICK_CONFIG_HEADER_FILE ${CMAKE_CURRENT_BINARY_DIR}/magick-config.h )
configure_file( ${CMAKE_CURRENT_SOURCE_DIR}/../config/config.h.cmake
	${MAGICK_CONFIG_HEADER_FILE}.step1 @ONLY )
configure_file( ${MAGICK_CONFIG_HEADER_FILE}.step1 ${MAGICK_CONFIG_HEADER_FILE} )

add_library(${LIBRARY_TARGET_NAME} 
	${${LIBRARY_TARGET_NAME}_SRCS} 
	${MAGICK_CONFIG_HEADER_FILE}
	${${LIBRARY_TARGET_NAME}_PRIVATE_HDRS} 
	${${LIBRARY_TARGET_NAME}_PUBLIC_HDRS} 
	${${LIBRARY_TARGET_NAME}_WIN32_XTRAS} 
	${CODERS_SRCS} )
target_include_directories(${LIBRARY_TARGET_NAME} PRIVATE ${CMAKE_CURRENT_BINARY_DIR}/.. ${CMAKE_CURRENT_SOURCE_DIR}/..)
target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC ${CODERS_DEPENDENT_LIBRARIES} )
target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC ${EXT_LINK_LIBS})
if(IMAGEMAGICK_WITH_GDCM)
    target_compile_definitions(${LIBRARY_TARGET_NAME} PRIVATE MAGICKCORE_GDCM_DELEGATE)
endif()

# Install targets
if(WIN32 AND BUILD_SHARED_LIBS)
	set_target_properties(${LIBRARY_TARGET_NAME} PROPERTIES IMPORT_SUFFIX _dll.lib )
	install(TARGETS ${LIBRARY_TARGET_NAME} ARCHIVE DESTINATION ${LIB_INSTALL_DIR})
endif()

install(TARGETS ${LIBRARY_TARGET_NAME} 
    EXPORT imagemagick-config
	DESTINATION ${LIB_INSTALL_DIR}
	INCLUDES DESTINATION include/imagemagick )
install( FILES ${${LIBRARY_TARGET_NAME}_PUBLIC_HDRS} ${MAGICK_CONFIG_HEADER_FILE}
	DESTINATION include/imagemagick )

